/*
 * Copyright 2019 - 2021 Aiven Oy
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
publishing {
    publications {
        maven(MavenPublication) {
            // Defaults, for clarity
            groupId = getGroup()
            artifactId = getName()
            version = getVersion()

            pom {
                name = "Aiven's OpenSearch Connector for Apache Kafka"
                description = "Aiven's OpenSearch Connector for Apache Kafka"
                url = "https://aiven.io"
                organization {
                    name = "Aiven Oy"
                    url = "https://aiven.io"
                }
                licenses {
                    license {
                        name = "Apache License 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.html"
                        distribution = "repo"

                    }
                }
                scm {
                    connection = "scm:git:git://github.com/aiven/aiven-kafka-connect-opensearch.git"
                    developerConnection = "scm:git:git@github.com:aiven/aiven-kafka-connect-opensearch.git"
                    url = "https://github.com/aiven/aiven-kafka-connect-opensearch.git"
                    tag = "HEAD"
                }
            }
        }
    }
}

archivesBaseName = "opensearch-kafka-connect"

sourceSets {
    integrationTest {
        java.srcDir file('src/integration-test/java')
        resources.srcDir file('src/integration-test/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

idea {
    module {
        testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
        testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

processResources {
    filesMatching('aiven-kafka-connect-opensearch-version.properties') {
        expand(version: version)
    }
}

dependencies {
    implementation project(":libs:client-configuration-spi")
    implementation project(":libs:client-basic-auth-configuration")
    compileOnly "org.apache.kafka:connect-api:$kafkaVersion"
    compileOnly "org.apache.kafka:connect-json:$kafkaVersion"

    implementation "org.slf4j:slf4j-api:$slf4jVersion"
    implementation "com.google.code.gson:gson:2.10.1"
    implementation "org.opensearch.client:opensearch-rest-high-level-client:$openSearchVersion"

    testImplementation "org.junit.jupiter:junit-jupiter:5.10.0"
    testImplementation "org.mockito:mockito-core:5.5.0"
    testImplementation "org.mockito:mockito-inline:5.2.0"
    testImplementation "org.mockito:mockito-junit-jupiter:5.5.0"

    testImplementation "org.apache.kafka:connect-api:$kafkaVersion"
    testImplementation "org.apache.kafka:connect-json:$kafkaVersion"
    testImplementation "com.fasterxml.jackson.core:jackson-core:2.15.3"
    testImplementation "com.fasterxml.jackson.core:jackson-databind:2.15.3"
    testImplementation "com.fasterxml.jackson.core:jackson-annotations:2.15.3"
    testRuntimeOnly "org.slf4j:slf4j-log4j12:$slf4jVersion"

    integrationTestImplementation "org.testcontainers:junit-jupiter:$testcontainersVersion"
    integrationTestImplementation "org.testcontainers:kafka:$testcontainersVersion" // this is not Kafka version
    integrationTestImplementation "org.opensearch:opensearch-testcontainers:2.0.0"
    integrationTestImplementation "org.apache.kafka:kafka-clients:$kafkaVersion:test"
    integrationTestImplementation "org.apache.kafka:connect-runtime:${kafkaVersion}"
    integrationTestImplementation "org.apache.kafka:connect-runtime:${kafkaVersion}:test"
    integrationTestImplementation "org.apache.kafka:kafka_2.13:${kafkaVersion}"
    integrationTestImplementation "org.apache.kafka:kafka_2.13:${kafkaVersion}:test"
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    dependsOn test, distTar

    useJUnitPlatform()

    // Run always.
    outputs.upToDateWhen { false }

    // Pass the distribution file path to the tests.
    systemProperty("integration-test.distribution.file.path", distTar.archiveFile.get().asFile.path)
    systemProperty("opensearch.testcontainers.image-version",
        project.findProperty("opensearch.testcontainers.image-version") ? project.getProperty("opensearch.testcontainers.image-version") : "2.0.0")
}

task connectorConfigDoc {
    description = "Generates the connector's configuration documentation."
    group = "documentation"
    dependsOn "classes"

    doLast {
        javaexec {
            main = "io.aiven.kafka.connect.opensearch.OpensearchSinkConnectorConfig"
            classpath = sourceSets.main.runtimeClasspath + sourceSets.main.compileClasspath
            standardOutput = new FileOutputStream("$projectDir/docs/opensearch-sink-connector-config-options.rst")
        }
    }
}

tasks.named(JavaBasePlugin.CHECK_TASK_NAME) {
  dependsOn tasks.named('integrationTest')
}
